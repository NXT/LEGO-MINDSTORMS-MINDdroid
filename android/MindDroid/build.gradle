apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def gitRepoUser = 'GIT_USER'
def gitRepoName = 'GIT_REPOSITORY'

android {
    compileSdkVersion sdkVersion

    defaultConfig {
        minSdkVersion minSdk
        targetSdkVersion sdkVersion
        versionCode getGitCommitCount()
        versionName getVersion()

        buildConfigField "String", gitRepoUser, "\"" + getGitUser() + "\""
        buildConfigField "String", gitRepoName, "\"" + getGitRepository() + "\""
    }

    signingConfigs {
        if (System.getenv("TRAVIS")) {
            release {
                println "I run on TRAVIS"
                storeFile file('../signing/release.keystore')
                storePassword System.getenv("KEYSTORE_PASS")
                keyAlias System.getenv("ALIAS_NAME")
                keyPassword System.getenv("ALIAS_PASS")
            }
        } else {
            release {
                def keystore = "${project.gradle.gradleUserHomeDir}/../.android/debug.keystore"
                println "I run local using $keystore"
                storePassword "android"
                keyPassword "android"
                keyAlias "androiddebugkey"
                storeFile file(keystore)
            }
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }

    lintOptions{
        disable 'MissingTranslation'
    }

}

static def getLatestGitHash() {
    def process = "git rev-parse --short HEAD".execute()
    return process.text.toString().trim()
}

static def getGitCommitCount() {
    def process = "git rev-list HEAD --count".execute()
    return process.text.toInteger()
}

static def getVersion() {
    return getTag() + "-" + getLatestGitHash()
}

static def getTag() {
    return "1.3" + "." + getGitCommitCount()
}

dependencies {
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.annotation:annotation:1.1.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'com.github.hannesa2:githubAppUpdate:0.1'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.2-1.3.60"
    implementation "androidx.lifecycle:lifecycle-runtime:2.2.0-rc03"
}


static def getGitOriginRemote() {
    def process = "git remote -v".execute()
    def values = process.text.toString().trim().split("\\r\\n|\\n|\\r")

    def found = values.find { it.startsWith("origin") && it.endsWith("(push)") }
    return found.replace("origin", "").replace("(push)", "").replace(".git", "").trim()
}

static def getGitUser() {
    def token = getGitOriginRemote().split("/")
    return token[3]
}

static def getGitRepository() {
    def token = getGitOriginRemote().split("/")
    return token[4]
}
